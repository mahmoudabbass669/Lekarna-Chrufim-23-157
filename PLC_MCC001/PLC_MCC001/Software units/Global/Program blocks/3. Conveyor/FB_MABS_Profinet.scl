NAMESPACE Global
FUNCTION_BLOCK FB_MABS_Profinet
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      MABS_ID : Int;
      CrossSensor : Bool;
      Fault : Bool;
      UpstreamRunning : Bool;
      Downstream_Straight : Bool;
      "Downstream_Right/Left" : Bool;
      SortDir : ItemDir;
      ManIn : MABS_ManIn;
      Setpoints : MABS_Setpoint;
   END_VAR

   VAR_IN_OUT 
      MABS_IBP_in : IB_P01_02_03_in_64bytes_struct;
      MABS_IBP_out : IB_P01_02_03_out_64bytes_struct;
   END_VAR

   VAR 
      ReadInputSignal : Struct
         CrossSensor : Bool;
         EmergencyStop : Bool;
         Fault : Bool;
      END_STRUCT;
      Active : Struct
         Line : Struct
            ProductionLine : Bool;
            EmptyLine : Bool;
            PackedLine : Bool;
         END_STRUCT;
         EstopZone { S7_SetPoint := 'False'} : EsopZone;
      END_STRUCT;
      CROSS_SENSOR_FILTER : FB_Sensor;
      MABS_SENSOR_BLOCKAGE {InstructionName := 'IEC_TIMER'; LibVersion := '1.0'} : IEC_TIMER;
      MABS_STATUS_ON : FB_StatusON;
      MABS_STATUS_READY : FB_StatusReady;
      MABS_STATUS_FAULT : FB_StatusFault;
      AUTO_RESET : FB_AutoReset;
      MABS_CONTROL : FB_MABS_PNIO;
      SSR : SystemSSR;
      ReadMabsStatus : ConveyorStatus;
      ReadMabsParameter : ConveyorParameters := ((), [2(T#10ms)], (), (), (), ());
      MABSFault : Fault;
      SimuMode : Struct
         Sensor : Bool;
         Fault : Bool;
      END_STRUCT;
      StartUpTimer {InstructionName := 'TOF_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TOF_TIME;
      FT_StartUp {InstructionName := 'F_TRIG'; LibVersion := '1.0'} : F_TRIG;
      SetStartUp { S7_SetPoint := 'True'} : Bool;
      VisuColor : Word;
      PhaseError : Bool;
      StartupMABS { S7_SetPoint := 'True'} : Bool;
      FilteredSensor { S7_SetPoint := 'True'} : Bool;
      AutoReset { S7_SetPoint := 'True'} : Bool;
      StopSystemFireActive : Bool;
      Sort { S7_SetPoint := 'False'} : ItemDir;
      Home : Bool;
      HomeCheckTimer {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      CurrentPosition : Struct
         left : Bool;
         right : Bool;
         straight : Bool;
      END_STRUCT;
      "Cmd+Setpoint" { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : MABS;
   END_VAR


BEGIN
	// ==========================================================================================================================================
	// Company        : LogTech, s.r.o.
	// Created by     : Anton Vinoth Soundraraj
	// Block          : FB_MABS
	// Project        : Lékarna DC Chrudim
	// Project number : 23-157
	// ==========================================================================================================================================
	
	REGION Active Line
	    
	    // ProductionLine
	    IF (#MABS_ID >= _.gminProductionLine AND
	        #MABS_ID <= _.gmaxProductionLine)
	    THEN
	        #Active.Line.ProductionLine := TRUE;
	    END_IF;
	    
	    // EmptyLine
	    IF (#MABS_ID >= _.gminEmptyLine AND
	        #MABS_ID <= _.gmaxEmptyLine)
	    THEN
	        #Active.Line.EmptyLine := TRUE;
	    END_IF;
	    
	    // PackedLine
	    IF (#MABS_ID >= _.gminPackedLine AND
	        #MABS_ID <= _.gmaxPackedLine)
	    THEN
	        #Active.Line.PackedLine := TRUE;
	    END_IF;
	    
	END_REGION
	
	REGION Estop zone
	    
	    // Production Line A1
	    IF  (#MABS_ID >= _."gA1.6.1" AND
	        #MABS_ID <= _."gA1.6.2") OR
	        // Empty Line A1
	        (#MABS_ID >= _."gJ1.5.2" AND
	        #MABS_ID <= _."gJ1.6.2")
	    THEN
	        #Active.EstopZone."1" := TRUE;
	    END_IF;
	    
	    // Production Line A2
	    IF  (#MABS_ID >= _."gA1.9.1" AND
	        #MABS_ID <= _."gA1.9.2")
	    THEN
	        #Active.EstopZone."2" := TRUE;
	    END_IF;
	    
	END_REGION
	
	REGION SystemSSR
	    
	    //Zone 1
	    IF #Active.EstopZone."1" THEN
	        #SSR.StartSystem := DB_SystemSSR.Zone."1".StartSystem;
	        #SSR.StopSystem := DB_SystemSSR.Zone."1".StopSystem;
	        #SSR.ResetSystem := DB_SystemSSR.Zone."1".ResetSystem;
	    END_IF;
	    
	    //Zone 2
	    IF #Active.EstopZone."2" THEN
	        #SSR.StartSystem := DB_SystemSSR.Zone."2".StartSystem;
	        #SSR.StopSystem := DB_SystemSSR.Zone."2".StopSystem;
	        #SSR.ResetSystem := DB_SystemSSR.Zone."2".ResetSystem;
	    END_IF;
	    
	END_REGION
	
	REGION Read Simu inputs  
	    
	    // ProductionLine
	    IF #Active.Line.ProductionLine THEN
	        // read simu
	        #SimuMode.Sensor := DB_SystemSimu.TriggerSensor.Line.ProductionLine.Conveyor[#MABS_ID];
	        #SimuMode.Fault := DB_SystemSimu.TriggerFault.Line.ProductionLine.Conveyor[#MABS_ID];
	    END_IF;
	    
	    // EmptyLine
	    IF #Active.Line.EmptyLine THEN
	        // read simu
	        #SimuMode.Sensor := DB_SystemSimu.TriggerSensor.Line.EmptyLine.Conveyor[#MABS_ID];
	        #SimuMode.Fault := DB_SystemSimu.TriggerFault.Line.EmptyLine.Conveyor[#MABS_ID];
	    END_IF;
	    
	    // PackedLine
	    IF #Active.Line.PackedLine THEN
	        // read simu
	        #SimuMode.Sensor := DB_SystemSimu.TriggerSensor.Line.PackedLine.Conveyor[#MABS_ID];
	        #SimuMode.Fault := DB_SystemSimu.TriggerFault.Line.PackedLine.Conveyor[#MABS_ID];
	    END_IF;
	    
	END_REGION
	
	REGION Read Input
	    
	    // read all inputs to the block
	    // Conveyor input read signals
	    #ReadInputSignal.CrossSensor := #CrossSensor OR  // Not
	    #SimuMode.Sensor; // simu call
	    #ReadInputSignal.Fault := #Fault OR
	    #SimuMode.Fault; // simu call;
	    #ReadInputSignal.EmergencyStop := // all estop zones are connected together
	    _.DB_SafetyStandard.ToStandard.EstopActive;
	    
	END_REGION
	
	REGION READ AND INIT
	    
	    REGION parameters
	        
	        // ProductionLine
	        IF #Active.Line.ProductionLine THEN
	            #ReadMabsParameter := DB_ConveyorParameters.Line.ProductionLine.Conveyor[#MABS_ID];
	        END_IF;
	        // EmptyLine
	        IF #Active.Line.EmptyLine THEN
	            #ReadMabsParameter := DB_ConveyorParameters.Line.EmptyLine.Conveyor[#MABS_ID];
	        END_IF;
	        // PackedLine
	        IF #Active.Line.PackedLine THEN
	            #ReadMabsParameter := DB_ConveyorParameters.Line.PackedLine.Conveyor[#MABS_ID];
	        END_IF;
	        
	    END_REGION
	    
	    REGION FIRE
	        
	        // Fire signal from fire system stops the whole system
	        // System can be reset from Fire status only if the fire signal is deactivated
	        
	        // FIRE ALARM ACTIVE --> STOP SYSTEM
	        IF DB_SystemSimu.Fire THEN
	            #StopSystemFireActive := TRUE;
	        END_IF;
	        // RESET --> FIRE ALARM
	        IF #SSR.ResetSystem AND NOT DB_SystemSimu.Fire THEN
	            #StopSystemFireActive := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	END_REGION
	
	REGION Conveyor startup
	    
	    // Conveyor StartUp: Conveyor startup signal after 5 second delay
	    
	    #StartUpTimer.TOF(IN := #SSR.StartSystem,
	                      PT := #ReadMabsParameter.StartUpTime);
	    
	    #FT_StartUp(CLK := #StartUpTimer.Q);
	    
	    // set startup after 5 seconds
	    #SetStartUp := #FT_StartUp.Q;
	    
	END_REGION
	
	REGION Conveyor Status
	    
	    REGION Status ON
	        
	        // ----------------------------------------->>>>>>> STATUS ON <<<<<<<-----------------------------------------
	        // FB_ConveyorStatusON
	        // called as IDMB
	        
	        #MABS_STATUS_ON(SystemStartActive := #SSR.StartSystem,
	                        StatusStop := #ReadMabsStatus.Stop,
	                        StatusEStop := #ReadMabsStatus.Estop,
	                        StatusManual := #ReadMabsStatus.Manual,
	                        StatusFault := #ReadMabsStatus.Fault,
	                        FireAlarmActive := #StopSystemFireActive,
	                        StatusON => #ReadMabsStatus.ON);
	        
	    END_REGION
	    
	    REGION Status Ready
	        
	        // ----------------------------------------->>>>>>> STATUS READY <<<<<<<-----------------------------------------
	        // FB_ConveyorStatusReady
	        // called as MIDB
	        
	        #MABS_STATUS_READY(StatusON := #ReadMabsStatus.ON,
	                           StatusESTOP := #ReadMabsStatus.Estop,
	                           StatusFAULT := #ReadMabsStatus.Fault,
	                           StatusMAN := #ReadMabsStatus.Manual,
	                           SignalFIRE := #StopSystemFireActive,
	                           SystemAlertTime := #ReadMabsParameter.StartUpTime,
	                           StatusReady => #ReadMabsStatus.ReadytoRun);
	        
	    END_REGION
	    
	    REGION Status Occupied
	        
	        // ----------------------------------------->>>>>>> STATUS OCCUPIED <<<<<<<-----------------------------------------
	        
	        #ReadMabsStatus.Occupied :=
	        #CROSS_SENSOR_FILTER.FilteredSensor;
	        
	    END_REGION
	    
	    REGION Status Running
	        
	        // ----------------------------------------->>>>>>> STATUS RUNNING <<<<<<<-----------------------------------------
	        
	        #ReadMabsStatus.Running :=
	        #ReadMabsStatus.ReadytoRun AND
	        #"Cmd+Setpoint".Run_balls;
	        
	    END_REGION
	    
	    REGION Status Fault
	        
	        // ----------------------------------------->>>>>>> STATUS FAULT <<<<<<<-----------------------------------------
	        
	        // Reading error signal from the motor drive
	        #MABSFault.Motor_Error := #ReadInputSignal.Fault;
	        
	        // FC_ConveyorStatusFault
	        #MABS_STATUS_FAULT(FaultActive := #MABSFault.Motor_Error,
	                           ResetFault := #SSR.ResetSystem,
	                           StatusRunning := #ReadMabsStatus.Running,
	                           FaultStatus => #ReadMabsStatus.Fault);
	        
	    END_REGION
	    
	    REGION Status ESTOP
	        
	        // ----------------------------------------->>>>>>> STATUS ESTOP <<<<<<<-----------------------------------------
	        
	        #AUTO_RESET(AckIN := #SSR.ResetSystem,
	                    Ack => #AutoReset);
	        
	        // EMERGENCY STOP ACTIVE --> STOP SYSTEM
	        IF #ReadInputSignal.EmergencyStop THEN
	            #ReadMabsStatus.Estop := TRUE;
	        END_IF;
	        // RESET --> EMERGENCY STOP
	        IF #AutoReset AND NOT #ReadInputSignal.EmergencyStop THEN
	            #ReadMabsStatus.Estop := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Status Stop
	        
	        // ----------------------------------------->>>>>>> STATUS STOP <<<<<<<-----------------------------------------
	        
	        IF #SSR.StopSystem THEN
	            #ReadMabsStatus.Stop := TRUE;
	        END_IF;
	        IF #SSR.StartSystem OR #SSR.ResetSystem THEN
	            #ReadMabsStatus.Stop := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Sensor Blockage
	        
	        // ----------------------------------------->>>>>>> STATUS BLOCKAGE <<<<<<<-------------------------------------
	        
	        // FB_SensorBlockage
	        // called as MIDB
	        
	        #MABS_SENSOR_BLOCKAGE.TON(IN := #ReadMabsStatus.ReadytoRun AND
	                                  #CROSS_SENSOR_FILTER.FilteredSensor,
	                                  PT := #ReadMabsParameter.SensorBlockageTime,
	                                  Q => #ReadMabsStatus.Blockage);
	        
	        // ==========================
	        // Sensor Blockage: HMI Alarm
	        // ==========================
	        // Sensor blockage alarm message is written to the first bit of the alarm word
	        // corresponding to the active line
	        
	        REGION alarm 
	            
	            // ==========================
	            // Sensor Blockage: HMI Alarm
	            // ==========================
	            // Sensor blockage alarm message is written to the alarm word
	            // corresponding to the active line
	            // ProductionLine
	            IF #Active.Line.ProductionLine THEN
	                DB_Alarm."word".Line.ProductionLine.Conveyor[#MABS_ID].%X0 := #ReadMabsStatus.Blockage;
	            END_IF;
	            // EmptyLine
	            IF #Active.Line.EmptyLine THEN
	               DB_Alarm."word".Line.EmptyLine.Conveyor[#MABS_ID].%X0 := #ReadMabsStatus.Blockage;
	            END_IF;
	            // PackedLine
	            IF #Active.Line.PackedLine THEN
	               DB_Alarm."word".Line.PackedLine.Conveyor[#MABS_ID].%X0 := #ReadMabsStatus.Blockage;
	            END_IF;
	            
	        END_REGION
	        
	    END_REGION
	    
	    REGION Visu Color
	        
	        // each and every status of the conveyor will be mentioned by different colors in HMI panel
	        
	        #VisuColor.%X8 := #ReadMabsStatus.Blockage;
	        #VisuColor.%X0 := #ReadMabsStatus.ON;
	        #VisuColor.%X1 := #ReadMabsStatus.ReadytoRun;
	        #VisuColor.%X2 := #ReadMabsStatus.Occupied;
	        #VisuColor.%X3 := #ReadMabsStatus.Running;
	        #VisuColor.%X4 := #ReadMabsStatus.Fault;
	        #VisuColor.%X5 := #ReadMabsStatus.Estop;
	        #VisuColor.%X6 := #ReadMabsStatus.Manual;
	        #VisuColor.%X7 := #ReadMabsStatus.Stop;
	        
	        // convert word to Interger value: 
	        // color will be depends on the status priority (integer value) 
	        // reference: Conveyor image in HMI screen -> property -> Apperance   
	        
	        // ProductionLine
	        IF #Active.Line.ProductionLine THEN
	            DB_VisuColor.Line.ProductionLine.Conveyor[#MABS_ID] :=
	            WORD_TO_INT(#VisuColor);
	        END_IF;
	        // EmptyLine
	        IF #Active.Line.EmptyLine THEN
	            DB_VisuColor.Line.EmptyLine.Conveyor[#MABS_ID] :=
	            WORD_TO_INT(#VisuColor);
	        END_IF;
	        // PackedLine
	        IF #Active.Line.PackedLine THEN
	            DB_VisuColor.Line.PackedLine.Conveyor[#MABS_ID] :=
	            WORD_TO_INT(#VisuColor);
	        END_IF;
	        
	    END_REGION
	    
	    REGION Global Status
	        
	        // write -> global status
	        // ProductionLine
	        IF #Active.Line.ProductionLine THEN
	            DB_Global.Status.Line.ProductionLine.On[#MABS_ID] := #ReadMabsStatus.ON;
	            DB_Global.Status.Line.ProductionLine.ReadyToRun[#MABS_ID] := #ReadMabsStatus.ReadytoRun;
	            DB_Global.Status.Line.ProductionLine.Occupied[#MABS_ID] := #ReadMabsStatus.Occupied;
	            DB_Global.Status.Line.ProductionLine.Running[#MABS_ID] := #ReadMabsStatus.Running;
	            DB_Global.Status.Line.ProductionLine.Fault[#MABS_ID] := #ReadMabsStatus.Fault;
	            DB_Global.Status.Line.ProductionLine.Estop[#MABS_ID] := #ReadMabsStatus.Estop;
	            DB_Global.Status.Line.ProductionLine.Manual[#MABS_ID] := #ReadMabsStatus.Manual;
	            DB_Global.Status.Line.ProductionLine.Stop[#MABS_ID] := #ReadMabsStatus.Stop;
	            DB_Global.Status.Line.ProductionLine.Blockage[#MABS_ID] := #ReadMabsStatus.Blockage;
	        END_IF;
	        // EmptyLine
	        IF #Active.Line.EmptyLine THEN
	            DB_Global.Status.Line.EmptyLine.On[#MABS_ID] := #ReadMabsStatus.ON;
	            DB_Global.Status.Line.EmptyLine.ReadyToRun[#MABS_ID] := #ReadMabsStatus.ReadytoRun;
	            DB_Global.Status.Line.EmptyLine.Occupied[#MABS_ID] := #ReadMabsStatus.Occupied;
	            DB_Global.Status.Line.EmptyLine.Running[#MABS_ID] := #ReadMabsStatus.Running;
	            DB_Global.Status.Line.EmptyLine.Fault[#MABS_ID] := #ReadMabsStatus.Fault;
	            DB_Global.Status.Line.EmptyLine.Estop[#MABS_ID] := #ReadMabsStatus.Estop;
	            DB_Global.Status.Line.EmptyLine.Manual[#MABS_ID] := #ReadMabsStatus.Manual;
	            DB_Global.Status.Line.EmptyLine.Stop[#MABS_ID] := #ReadMabsStatus.Stop;
	            DB_Global.Status.Line.EmptyLine.Blockage[#MABS_ID] := #ReadMabsStatus.Blockage;
	        END_IF;
	        // PackedLine
	        IF #Active.Line.PackedLine THEN
	            DB_Global.Status.Line.PackedLine.On[#MABS_ID] := #ReadMabsStatus.ON;
	            DB_Global.Status.Line.PackedLine.ReadyToRun[#MABS_ID] := #ReadMabsStatus.ReadytoRun;
	            DB_Global.Status.Line.PackedLine.Occupied[#MABS_ID] := #ReadMabsStatus.Occupied;
	            DB_Global.Status.Line.PackedLine.Running[#MABS_ID] := #ReadMabsStatus.Running;
	            DB_Global.Status.Line.PackedLine.Fault[#MABS_ID] := #ReadMabsStatus.Fault;
	            DB_Global.Status.Line.PackedLine.Estop[#MABS_ID] := #ReadMabsStatus.Estop;
	            DB_Global.Status.Line.PackedLine.Manual[#MABS_ID] := #ReadMabsStatus.Manual;
	            DB_Global.Status.Line.PackedLine.Stop[#MABS_ID] := #ReadMabsStatus.Stop;
	            DB_Global.Status.Line.PackedLine.Blockage[#MABS_ID] := #ReadMabsStatus.Blockage;
	        END_IF;
	        
	    END_REGION
	    
	END_REGION
	
	REGION Sorter sensor filter
	    
	    // cross sensor filter
	    #CROSS_SENSOR_FILTER(Sensor :=  #ReadInputSignal.CrossSensor,   ///#CrossSensor
	                         SensorFilterTime := #ReadMabsParameter.SensorFilterTime,
	                         FilteredSensor=> #FilteredSensor);
	    
	END_REGION
	
	REGION MABS LOGIC
	    
	    REGION rcv direction 
	        
	        IF NOT #FilteredSensor THEN
	            #Sort := #SortDir;
	        END_IF;
	        
	    END_REGION
	    
	    REGION MABS lib block
	        
	        #MABS_CONTROL(IBP_in  := #MABS_IBP_in,
	                      IBP_out := #MABS_IBP_out,
	                      MABS    := #"Cmd+Setpoint");
	        
	    END_REGION
	    
	    REGION run MABS wheels
	        
	        #"Cmd+Setpoint".Run_balls := #UpstreamRunning;
	        
	    END_REGION
	    
	    REGION Current position
	        
	        #CurrentPosition.left := #"Cmd+Setpoint".Left_Pos_reach;
	        #CurrentPosition.right := #"Cmd+Setpoint".Right_Pos_reach;
	        #CurrentPosition.straight := #"Cmd+Setpoint".Straight_Pos_reach;
	        
	    END_REGION
	    
	    REGION HOMING
	        
	        // ==========================================================================================================================
	        // Initial operation (home searching)
	        // ==================================
	        
	        // Home missing (if MABS could not receive any position in 't' duration)
	        #HomeCheckTimer.TON(IN := NOT #CurrentPosition.left AND
	                            NOT #CurrentPosition.right AND
	                            NOT #CurrentPosition.straight,
	                            PT := T#2s);
	        #Home :=
	        NOT #HomeCheckTimer.Q;
	        
	        // init HOME
	        IF NOT #Home AND NOT #ReadMabsStatus.Stop THEN
	            #"Cmd+Setpoint".Init := TRUE;
	        END_IF;
	        // RESET -> Init Home
	        IF #Home THEN
	            #"Cmd+Setpoint".Init := TRUE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Parameters
	        
	        // change balls direction
	        #"Cmd+Setpoint".Dir_balls := #Setpoints.ChangeBallsDirection;
	        // balls speed
	        #"Cmd+Setpoint".Speed_balls := #Setpoints.Speed_balls;
	        // offset for straight
	        #"Cmd+Setpoint".Init_offset := #Setpoints.Init_offset;
	        
	    END_REGION
	    
	    REGION Sorting logic
	        
	        IF #ReadMabsStatus.ReadytoRun
	        THEN
	            
	            REGION Straight
	                
	                // ================================================== ^^^
	                
	                // sort straight - actual position: right
	                
	                IF #Sort.Straight AND
	                    NOT #CurrentPosition.straight AND
	                    #CurrentPosition.right AND
	                    NOT #CROSS_SENSOR_FILTER.FilteredSensor
	                THEN
	                    #"Cmd+Setpoint".Switch_right_to_st := TRUE;
	                ELSE
	                    #"Cmd+Setpoint".Switch_right_to_st := FALSE;
	                END_IF;
	                
	                // sort straight - actual position: left
	                
	                IF #Sort.Straight AND
	                    NOT #CurrentPosition.straight AND
	                    #CurrentPosition.left AND
	                    NOT #CROSS_SENSOR_FILTER.FilteredSensor
	                THEN
	                    #"Cmd+Setpoint".Switch_left_to_st := TRUE;
	                ELSE
	                    #"Cmd+Setpoint".Switch_left_to_st := FALSE;
	                END_IF;
	                
	            END_REGION
	            
	            REGION to Right
	                
	                // ================================================== >>>
	                
	                // sort right - actual position: straight/left
	                
	                IF #Sort.toRight AND
	                    NOT #CurrentPosition.right AND
	                    (#CurrentPosition.straight OR
	                    #CurrentPosition.left) AND
	                    NOT #CROSS_SENSOR_FILTER.FilteredSensor
	                THEN
	                    #"Cmd+Setpoint".Switch_right := TRUE;
	                ELSE
	                    #"Cmd+Setpoint".Switch_right := FALSE;
	                END_IF;
	                
	            END_REGION
	            
	            REGION to left
	                
	                // ================================================== <<<
	                
	                // sort left - actual position: straight/right
	                
	                IF #Sort.toLeft AND
	                    NOT #CurrentPosition.left AND
	                    (#CurrentPosition.straight OR
	                    #CurrentPosition.right) AND
	                    NOT #CROSS_SENSOR_FILTER.FilteredSensor
	                THEN
	                    #"Cmd+Setpoint".Switch_left := TRUE;
	                ELSE
	                    #"Cmd+Setpoint".Switch_left := FALSE;
	                END_IF;
	                
	            END_REGION
	            
	            // ============================================================================================================
	            
	        END_IF;
	        
	    END_REGION
	    
	   REGION reset error 
	        
	        // reset error
	       IF #ReadMabsStatus.Fault AND
	           #SSR.ResetSystem
	       THEN
	            #"Cmd+Setpoint".Reset_error := TRUE;
	        ELSE
	            #"Cmd+Setpoint".Reset_error := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION stop function
	        
	        // stop if not ready
	        IF NOT #ReadMabsStatus.ReadytoRun AND NOT #ReadMabsStatus.Manual
	        THEN
	            #"Cmd+Setpoint".Switch_left := FALSE;
	            #"Cmd+Setpoint".Switch_right := FALSE;
	            #"Cmd+Setpoint".Switch_right_to_st := FALSE;
	            #"Cmd+Setpoint".Switch_left_to_st := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	END_REGION
	
	
	
END_FUNCTION_BLOCK
END_NAMESPACE

