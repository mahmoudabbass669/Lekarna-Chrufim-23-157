NAMESPACE LineB1
FUNCTION_BLOCK FB_S300_FullControl
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      TransferID : Int;
      TransferSensor : Bool;
      BeltUp : Bool;
      RollerUp : Bool;
      Fault : Bool;
      ReadyToRCV : Bool;
      UpstreamStraight : Bool;
      UpstreamArm : Bool;
      FrontSensor : Bool;
      EndSensor : Bool;
      "Left/RightSensor" : Bool;
      Downstream : Bool;
      PriorityStraight : Bool;
      PriorityArm : Bool;
      FrontSensorFilterTime : Array[1..2] of Time;
   END_VAR

   VAR_OUTPUT 
      Direction : Bool;
      Cmd1 : Bool;
      Cmd2 : Bool;
      ErrorReset : Bool;
   END_VAR

   VAR 
      ReadInputSignal : Struct
         EmergencyStop : Bool;
         FireActive : Bool;
         Fault : Bool;
         TransferSensor : Bool;
      END_STRUCT;
      Active : Struct
         Line : Struct
            B1 : Bool;
         END_STRUCT;
      END_STRUCT;
      TRANSFER_SENSOR : _.Global.FB_Sensor;
      FRONT_SENSOR_FILTER : _.Global.FB_Sensor;
      TRANSFER_SENSOR_BLOCKAGE {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      FRONT_SENSOR_BLOCKAGE {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      END_SENSOR_BLOCKAGE {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      "LEFT/RIGHT_SENSOR_BLOCKAGE" {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      TRANSFER_STATUS_ON : _.Global.FB_StatusON;
      TRANSFER_STATUS_READY : _.Global.FB_StatusReady;
      TRANSFER_STATUS_FAULT : _.Global.FB_StatusFault;
      AUTO_RESET : _.Global.FB_AutoReset;
      StartUpTimer {InstructionName := 'TOF_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TOF_TIME;
      FT_StartUp {InstructionName := 'F_TRIG'; LibVersion := '1.0'} : F_TRIG;
      SetStartUp { S7_SetPoint := 'True'} : Bool;
      Init : Struct
         Line : Struct
            A1 : String := 'LineA1';
         END_STRUCT;
      END_STRUCT;
      SSR : _.Global.SystemSSR;
      ReadTransferStatus : _.Global.ConveyorStatus;
      ReadTransferParameters : _.Global.ConveyorParameters := ((), [2(T#10ms)], (), (), ());
      ConveyorFault : _.Global.Fault;
      StartupTransfer { S7_SetPoint := 'True'} : Bool;
      FilteredSensor { S7_SetPoint := 'True'} : Bool;
      StopSystemFireActive : Bool;
      ClearTransferArea : Bool;
      AutoReset : Bool;
      VisuColor : Word;
      Transfer : Struct
         ErrorReset : Bool;
         Direction : Bool;
         Command_1 : Bool;
         Command_2 : Bool;
      END_STRUCT;
   END_VAR


BEGIN
	// ==========================================================================================================================================
	// Company        : LogTech, s.r.o.
	// Created by     : Anton Vinoth Soundraraj
	// Block          : FB_S300_tTransMode
	// Project        : Lékarna DC Chrudim
	// Project number : 23-157
	// ==========================================================================================================================================
	
	REGION Active Line
	    
	        #Active.Line.B1 := TRUE;
	        
	END_REGION
	
	REGION SystemSSR
	    
	    // Line B1
	    IF #Active.Line.B1 THEN
	        #SSR.StartSystem := _.Global.DB_SystemSSR.Line.B1.StartSystem;
	        #SSR.StopSystem := _.Global.DB_SystemSSR.Line.B1.StopSystem;
	        #SSR.ResetSystem := _.Global.DB_SystemSSR.Line.B1.ResetSystem;
	    END_IF;
	    
	END_REGION
	
	REGION Read Input
	    
	    // read all inputs to the block
	    // Conveyor input read signals
	    #ReadInputSignal.TransferSensor := #TransferSensor;
	    #ReadInputSignal.Fault := #Fault;
	    // Read static tag connections
	    #ReadInputSignal.EmergencyStop := // all estop zones are connected together
	    _.DB_SafetyStandard.ToStandard.EstopActive;
	    // Common system input Read
	    #ReadInputSignal.FireActive :=
	    _.Global.DB_SystemSimu.Fire; // simu call
	    
	END_REGION
	
	REGION Conveyor startup
	    
	    // Conveyor StartUp: Conveyor startup signal after preset time
	    #StartUpTimer.TOF(IN := #SSR.StartSystem,
	                      PT := #ReadTransferParameters.StartUpTime);
	    
	    #FT_StartUp(CLK := #StartUpTimer.Q);
	    
	    // set startup after the preset
	    #SetStartUp := #FT_StartUp.Q;
	    
	END_REGION
	
	REGION READ AND INIT
	    
	    REGION Init conveyor parameters
	        
	        // Conveyor Parameter
	        // Line B1
	        IF #Active.Line.B1 THEN
	            #ReadTransferParameters := DB_ConveyorParameters.Line.B1.Conveyor[#TransferID];
	        END_IF;
	        
	    END_REGION
	    
	    REGION Sensor Filter
	        
	        // FB_Sensor
	        // called as MIDB
	        
	        #TRANSFER_SENSOR(Sensor := #ReadInputSignal.TransferSensor,
	                         SensorFilterTime := #ReadTransferParameters.SensorFilterTime,
	                         FilteredSensor => #FilteredSensor);
	        
	        // front sensor filter
	        #FRONT_SENSOR_FILTER(Sensor := #FrontSensor,
	                             SensorFilterTime := #FrontSensorFilterTime);
	        
	    END_REGION
	    
	    REGION FIRE
	        
	        // Fire signal from fire system stops the whole system
	        // System can be reset from Fire status only if the fire signal is deactivated
	        
	        // FIRE ALARM ACTIVE --> STOP SYSTEM
	        IF #ReadInputSignal.FireActive THEN
	            #StopSystemFireActive := TRUE;
	        END_IF;
	        // RESET --> FIRE ALARM
	        IF #SSR.ResetSystem AND NOT #ReadInputSignal.FireActive THEN
	            #StopSystemFireActive := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	END_REGION
	
	REGION Conveyor Status
	    
	    REGION Status ON
	        
	        // ----------------------------------------->>>>>>> STATUS ON <<<<<<<-----------------------------------------
	        // FB_ConveyorStatusON
	        // called as IDMB
	        
	        #TRANSFER_STATUS_ON(SystemStartActive := #SSR.StartSystem,
	                            StatusStop := #ReadTransferStatus.Stop,
	                            StatusEStop := #ReadTransferStatus.Estop,
	                            StatusManual := #ReadTransferStatus.Manual,
	                            StatusFault := #ReadTransferStatus.Fault,
	                            FireAlarmActive := #StopSystemFireActive,
	                            StatusON => #ReadTransferStatus.ON);
	        
	    END_REGION
	    
	    REGION Status Ready
	        
	        // ----------------------------------------->>>>>>> STATUS READY <<<<<<<-----------------------------------------
	        // FB_ConveyorStatusReady
	        // called as MIDB
	        
	        #TRANSFER_STATUS_READY(StatusON := #ReadTransferStatus.ON,
	                               StatusESTOP := #ReadTransferStatus.Estop,
	                               StatusFAULT := #ReadTransferStatus.Fault,
	                               StatusMAN := #ReadTransferStatus.Manual,
	                               SignalFIRE := #StopSystemFireActive,
	                               SystemAlertTime := #ReadTransferParameters.StartUpTime,
	                               StatusReady => #ReadTransferStatus.ReadytoRun);
	        
	    END_REGION
	    
	    REGION Status Occupied
	        
	        // ----------------------------------------->>>>>>> STATUS OCCUPIED <<<<<<<-----------------------------------------
	        
	        #ReadTransferStatus.Occupied :=
	        #FilteredSensor AND
	        NOT #FRONT_SENSOR_FILTER.FilteredSensor;
	        
	    END_REGION
	    
	    REGION Status Running
	        
	        // ----------------------------------------->>>>>>> STATUS RUNNING <<<<<<<-----------------------------------------
	        
	        #ReadTransferStatus.Running := #Transfer.Command_1 OR #Transfer.Command_2;
	        
	    END_REGION
	    
	    REGION Status Fault
	        
	        // Automatic reset trigger to the needed drive after wakeup from ESTOP power miss (only if needed)
	        // will trigger for every Reset active
	        
	        #AUTO_RESET(AckIN := #SSR.ResetSystem,
	                    Ack => #AutoReset);
	        
	        // ----------------------------------------->>>>>>> STATUS FAULT <<<<<<<-----------------------------------------
	        
	        #TRANSFER_STATUS_FAULT(FaultActive := #ReadInputSignal.Fault,
	                               ResetFault := #SSR.ResetSystem OR #AutoReset,
	                               StatusRunning := #ReadTransferStatus.Running,
	                               FaultStatus => #ReadTransferStatus.Fault);
	        
	        #ErrorReset := #SSR.ResetSystem AND #ReadTransferStatus.Fault;
	        
	    END_REGION
	    
	    REGION Status ESTOP
	        
	        // ----------------------------------------->>>>>>> STATUS ESTOP <<<<<<<-----------------------------------------
	        
	        // EMERGENCY STOP ACTIVE --> STOP SYSTEM
	        IF #ReadInputSignal.EmergencyStop THEN
	            #ReadTransferStatus.Estop := TRUE;
	        END_IF;
	        // RESET --> EMERGENCY STOP
	        IF #AutoReset AND NOT #ReadInputSignal.EmergencyStop THEN
	            #ReadTransferStatus.Estop := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Status Manual
	        
	        // ----------------------------------------->>>>>>> STATUS MANUAL <<<<<<<-----------------------------------------
	        
	        // Write to static from IO
	        // ACTIVATE --> MANUAL MODE
	        // Line B1
	        IF #Active.Line.B1 THEN
	            // SET --> MANUAL MODE
	            IF DB_Manual.Line.B1.Conveyor[#TransferID].ManualEnable THEN
	                #ReadTransferStatus.Manual := TRUE;
	            END_IF;
	            // RESET --> MANUAL MODE
	            IF NOT DB_Manual.Line.B1.Conveyor[#TransferID].ManualEnable AND #SSR.ResetSystem THEN
	                #ReadTransferStatus.Manual := FALSE;
	            END_IF;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Status Stop
	        
	        // ----------------------------------------->>>>>>> STATUS STOP <<<<<<<-----------------------------------------
	        
	        IF #SSR.StopSystem THEN
	            #ReadTransferStatus.Stop := TRUE;
	        END_IF;
	        IF #SSR.StartSystem OR #SSR.ResetSystem THEN
	            #ReadTransferStatus.Stop := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Status Blockage
	        
	        // FB_SensorBlockage
	        // called as MIDB
	        #TRANSFER_SENSOR_BLOCKAGE(IN := #ReadTransferStatus.ReadytoRun AND #FilteredSensor AND NOT #Downstream,
	                                  PT := T#15s);
	        
	        #FRONT_SENSOR_BLOCKAGE(IN := #ReadTransferStatus.ReadytoRun AND #FrontSensor AND NOT #Downstream,
	                               PT := T#15s);
	        
	        #END_SENSOR_BLOCKAGE(IN := #ReadTransferStatus.ReadytoRun AND #EndSensor AND NOT #Downstream,
	                             PT := T#15s);
	        
	        #"LEFT/RIGHT_SENSOR_BLOCKAGE"(IN := #ReadTransferStatus.ReadytoRun AND #"Left/RightSensor" AND NOT #Downstream,
	                                      PT := T#5s);
	        
	        #ReadTransferStatus.Blockage :=
	        (#TRANSFER_SENSOR_BLOCKAGE.Q OR
	        #FRONT_SENSOR_BLOCKAGE.Q OR
	        #END_SENSOR_BLOCKAGE.Q OR
	        #"LEFT/RIGHT_SENSOR_BLOCKAGE".Q);
	        
	        // ==========================
	        // Sensor Blockage: HMI Alarm
	        // ==========================
	        // Sensor blockage alarm message is written to the alarm word
	        // corresponding to the active line
	        
	        REGION alarm 
	            
	            // ==========================
	            // Sensor Blockage: HMI Alarm
	            // ==========================
	            // Sensor blockage alarm message is written to the alarm word
	            // corresponding to the active line
	            // Line B1
	            IF #Active.Line.B1 THEN
	                DB_Alarm."word".Line.B1.Conveyor[#TransferID].%X0 := #ReadTransferStatus.Blockage;
	            END_IF;
	            
	        END_REGION
	        
	    END_REGION
	    
	    REGION Visu Color
	        
	        // each and every status of the conveyor will be mentioned by different colors in HMI panel
	        
	        #VisuColor.%X8 := #ReadTransferStatus.Blockage;
	        #VisuColor.%X0 := #ReadTransferStatus.ON;
	        #VisuColor.%X1 := #ReadTransferStatus.ReadytoRun;
	        #VisuColor.%X2 := #ReadTransferStatus.Occupied;
	        #VisuColor.%X3 := #ReadTransferStatus.Running;
	        #VisuColor.%X4 := #ReadTransferStatus.Fault;
	        #VisuColor.%X5 := #ReadTransferStatus.Estop;
	        #VisuColor.%X6 := #ReadTransferStatus.Manual;
	        #VisuColor.%X7 := #ReadTransferStatus.Stop;
	        
	        // convert word to Interger value: 
	        // color will be depends on the status priority (integer value) 
	        // reference: Conveyor image in HMI screen -> property -> Apperance   
	        
	        // Line B1
	        IF #Active.Line.B1 THEN
	            _.Global.DB_VisuColor.Line.B1.Conveyor[#TransferID] :=
	            WORD_TO_INT(#VisuColor);
	        END_IF;
	        
	    END_REGION
	    
	    REGION Global Status
	        
	        // write -> global status
	        // Line B1
	        IF #Active.Line.B1 THEN
	            _.Global.DB_Global.Status.Line.B1.On[#TransferID] := #ReadTransferStatus.ON;
	            _.Global.DB_Global.Status.Line.B1.ReadyToRun[#TransferID] := #ReadTransferStatus.ReadytoRun;
	            _.Global.DB_Global.Status.Line.B1.Occupied[#TransferID] := #ReadTransferStatus.Occupied;
	            _.Global.DB_Global.Status.Line.B1.Running[#TransferID] := #ReadTransferStatus.Running;
	            _.Global.DB_Global.Status.Line.B1.Fault[#TransferID] := #ReadTransferStatus.Fault;
	            _.Global.DB_Global.Status.Line.B1.Estop[#TransferID] := #ReadTransferStatus.Estop;
	            _.Global.DB_Global.Status.Line.B1.Manual[#TransferID] := #ReadTransferStatus.Manual;
	            _.Global.DB_Global.Status.Line.B1.Stop[#TransferID] := #ReadTransferStatus.Stop;
	            _.Global.DB_Global.Status.Line.B1.Blockage[#TransferID] := #ReadTransferStatus.Blockage;
	        END_IF;
	        
	    END_REGION
	    
	END_REGION
	
	REGION Transfer Logic
	    
	    IF #ReadTransferStatus.ReadytoRun THEN
	        
	        REGION Belt going UP
	            
	            // tote from straight has higher priority
	            // upstream straight is satisfied (add upstream sensor and front sensor)
	            // transfer is not occupied
	            // belt is not UP
	            IF (#PriorityStraight AND
	                #UpstreamStraight AND
	                NOT #ReadTransferStatus.Occupied AND
	                NOT #BeltUp) OR
	                // transfer has received an item from arm
	                // belt is not UP
	                (#ReadTransferStatus.Occupied AND
	                NOT #BeltUp)
	            THEN
	                #Transfer.Command_1 := TRUE;
	                #Transfer.Command_2 := TRUE;
	                #Transfer.Direction := TRUE;
	            END_IF;
	            
	        END_REGION
	        
	        REGION Roller going UP
	            
	            // tote from arm has higher priority
	            // upstream arm is satisfied (add upstream sensor and left/right sensor)
	            // transfer is not occupied
	            // roller is not UP
	            IF (#PriorityArm AND
	                #UpstreamArm AND
	                NOT #ReadTransferStatus.Occupied AND
	                NOT #RollerUp)
	            THEN
	                #Transfer.Command_1 := TRUE;
	                #Transfer.Command_2 := TRUE;
	                #Transfer.Direction := FALSE;
	            END_IF;
	            
	        END_REGION
	        
	        REGION Belt rcv and trs item
	            
	            IF // belt rcv the item (from straight)
	                (#BeltUp AND
	                NOT #ReadTransferStatus.Occupied AND
	                #PriorityStraight AND
	                #UpstreamStraight) OR
	                // belt trs the item
	                (#BeltUp AND
	                #ReadTransferStatus.Occupied AND
	                NOT #Downstream)
	            THEN
	                #Transfer.Command_1 := TRUE;
	                #Transfer.Command_2 := FALSE;
	                #Transfer.Direction := FALSE;
	            END_IF;
	            
	        END_REGION
	        
	    END_IF;
	    
	    REGION TRANSFER OFF
	        
	        IF NOT #ReadTransferStatus.ReadytoRun OR
	            (#ReadTransferStatus.Occupied AND
	            #Downstream)
	        THEN
	            #Transfer.Command_1 := FALSE;
	            #Transfer.Command_2 := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Write output
	        
	        // Static to output
	        
	        #Direction := #Transfer.Direction;
	        #Cmd1 := #Transfer.Command_1;
	        #Cmd2 := #Transfer.Command_2;
	        #ErrorReset := #Transfer.ErrorReset;
	        
	    END_REGION
	    
	END_REGION
	
	
	
	
	
END_FUNCTION_BLOCK
END_NAMESPACE

