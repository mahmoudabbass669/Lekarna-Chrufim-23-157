NAMESPACE Global
FUNCTION_BLOCK FB_S300_tTransMode
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      TransferID : Int;
      TransferSensor : Bool;
      Fault : Bool;
      ReadyToRCV : Bool;
      Upstream : Bool;
      FrontSensor : Bool;
      EndSensor : Bool;
      "Left/RightSensor" : Bool;
      Downstream : Bool;
      FrontSensorFilterTime : Array[1..2] of Time;
      TransferDirection : ItemDir;
   END_VAR

   VAR_OUTPUT 
      StartSignal : Bool;
      Cmd1 : Bool;
      Cmd2 : Bool;
      ErrorReset : Bool;
   END_VAR

   VAR 
      ReadInputSignal : Struct
         EmergencyStop : Bool;
         FireActive : Bool;
         Fault : Bool;
         TransferSensor : Bool;
      END_STRUCT;
      Active : Struct
         Line : Struct
            A1 : Bool;
            B1 : Bool;
            C1 : Bool;
            D1 : Bool;
         END_STRUCT;
      END_STRUCT;
      TRANSFER_SENSOR : FB_Sensor;
      FRONT_SENSOR_FILTER : FB_Sensor;
      TRANSFER_SENSOR_BLOCKAGE {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      FRONT_SENSOR_BLOCKAGE {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      END_SENSOR_BLOCKAGE {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      "LEFT/RIGHT_SENSOR_BLOCKAGE" {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      TRANSFER_STATUS_ON : FB_StatusON;
      TRANSFER_STATUS_READY : FB_StatusReady;
      TRANSFER_STATUS_FAULT : FB_StatusFault;
      AUTO_RESET : FB_AutoReset;
      StartUpTimer {InstructionName := 'TOF_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TOF_TIME;
      FT_StartUp {InstructionName := 'F_TRIG'; LibVersion := '1.0'} : F_TRIG;
      SetStartUp { S7_SetPoint := 'True'} : Bool;
      Init : Struct
         Line : Struct
            A1 : String := 'LineA1';
         END_STRUCT;
      END_STRUCT;
      SSR : SystemSSR;
      ReadTransferStatus : ConveyorStatus;
      ReadTransferParameters : ConveyorParameters := ((), [2(T#10ms)], (), (), ());
      ConveyorFault : Fault;
      StartupTransfer { S7_SetPoint := 'True'} : Bool;
      FilteredSensor { S7_SetPoint := 'True'} : Bool;
      StopSystemFireActive : Bool;
      ClearTransferArea : Bool;
      AutoReset : Bool;
      VisuColor : Word;
      Transfer : Struct
         RcvInfeed : Bool;
         StartSignal : Bool;
         Command_1 : Bool;
         Command_2 : Bool;
      END_STRUCT;
      NegEdgeUpstream {InstructionName := 'F_TRIG'; LibVersion := '1.0'; S7_SetPoint := 'False'} : F_TRIG;
      CrossSensorFilterTime { S7_SetPoint := 'True'} : Array[0..1] of Time;
      sTransferDirection { S7_SetPoint := 'False'} : ItemDir;
   END_VAR


BEGIN
	// ==========================================================================================================================================
	// Company        : LogTech, s.r.o.
	// Created by     : Anton Vinoth Soundraraj
	// Block          : FB_S300_tTransMode
	// Project        : Lékarna DC Chrudim
	// Project number : 23-157
	// ==========================================================================================================================================
	
	REGION Active Line
	    
	    // Line A1
	    IF (#TransferID >= _.gMinA1 AND
	        #TransferID <= _.gMaxA1)
	    THEN
	        #Active.Line.A1 := TRUE;
	    END_IF;
	    
	    // Line B1
	    IF (#TransferID >= _.gMinB1 AND
	        #TransferID <= _.gMaxB1)
	    THEN
	        #Active.Line.B1 := TRUE;
	    END_IF;
	    
	    // Line C1
	    IF (#TransferID >= _.gMinC1 AND
	        #TransferID <= _.gMaxC1)
	    THEN
	        #Active.Line.C1 := TRUE;
	    END_IF;
	    
	    // Line D1
	    IF (#TransferID >= _.gMinD1 AND
	        #TransferID <= _.gMaxD1)
	    THEN
	        #Active.Line.A1 := TRUE;
	    END_IF;
	    
	END_REGION
	
	REGION SystemSSR
	    
	    // Line A1
	    IF #Active.Line.A1 THEN
	        #SSR.StartSystem := DB_SystemSSR.Line.A1.StartSystem;
	        #SSR.StopSystem := DB_SystemSSR.Line.A1.StopSystem;
	        #SSR.ResetSystem := DB_SystemSSR.Line.A1.ResetSystem;
	    END_IF;
	    
	    // Line B1
	    IF #Active.Line.B1 THEN
	        #SSR.StartSystem := DB_SystemSSR.Line.B1.StartSystem;
	        #SSR.StopSystem := DB_SystemSSR.Line.B1.StopSystem;
	        #SSR.ResetSystem := DB_SystemSSR.Line.B1.ResetSystem;
	    END_IF;
	    
	    // Line C1
	    IF #Active.Line.C1 THEN
	        #SSR.StartSystem := DB_SystemSSR.Line.C1.StartSystem;
	        #SSR.StopSystem := DB_SystemSSR.Line.C1.StopSystem;
	        #SSR.ResetSystem := DB_SystemSSR.Line.C1.ResetSystem;
	    END_IF;
	    
	    // Line D1
	    IF #Active.Line.D1 THEN
	        #SSR.StartSystem := DB_SystemSSR.Line.D1.StartSystem;
	        #SSR.StopSystem := DB_SystemSSR.Line.D1.StopSystem;
	        #SSR.ResetSystem := DB_SystemSSR.Line.D1.ResetSystem;
	    END_IF;
	    
	END_REGION
	
	REGION Read Input
	    
	    // read all inputs to the block
	    // Conveyor input read signals
	    #ReadInputSignal.TransferSensor := #TransferSensor;
	    #ReadInputSignal.Fault := #Fault;
	    // Read static tag connections
	    #ReadInputSignal.EmergencyStop := // all estop zones are connected together
	    _.DB_SafetyStandard.ToStandard.EstopActive;
	    // Common system input Read
	    #ReadInputSignal.FireActive :=
	    DB_SystemSimu.Fire; // simu call
	    
	END_REGION
	
	REGION Conveyor startup
	    
	    // Conveyor StartUp: Conveyor startup signal after preset time
	    #StartUpTimer.TOF(IN := #SSR.StartSystem,
	                      PT := #ReadTransferParameters.StartUpTime);
	    
	    #FT_StartUp(CLK := #StartUpTimer.Q);
	    
	    // set startup after the preset
	    #SetStartUp := #FT_StartUp.Q;
	    
	END_REGION
	
	REGION READ AND INIT
	    
	    REGION Init conveyor parameters
	        
	        // Conveyor Parameter
	        // Line A1
	        IF #Active.Line.A1 THEN
	            #ReadTransferParameters := DB_ConveyorParameters.Line.A1.Conveyor[#TransferID];
	        END_IF;
	        // Line B1
	        IF #Active.Line.B1 THEN
	            #ReadTransferParameters := DB_ConveyorParameters.Line.B1.Conveyor[#TransferID];
	        END_IF;
	        // Line C1
	        IF #Active.Line.C1 THEN
	            #ReadTransferParameters := DB_ConveyorParameters.Line.C1.Conveyor[#TransferID];
	        END_IF;
	        // Line D1
	        IF #Active.Line.D1 THEN
	            #ReadTransferParameters := DB_ConveyorParameters.Line.D1.Conveyor[#TransferID];
	        END_IF;
	        
	    END_REGION
	    
	    REGION Sensor Filter
	        
	        // FB_Sensor
	        // called as MIDB
	        
	        #TRANSFER_SENSOR(Sensor := #ReadInputSignal.TransferSensor,
	                         SensorFilterTime := #ReadTransferParameters.SensorFilterTime,
	                         FilteredSensor => #FilteredSensor);
	        
	        // front sensor filter
	        #FRONT_SENSOR_FILTER(Sensor := #FrontSensor,
	                             SensorFilterTime := #FrontSensorFilterTime);
	        
	    END_REGION
	    
	    REGION FIRE
	        
	        // Fire signal from fire system stops the whole system
	        // System can be reset from Fire status only if the fire signal is deactivated
	        
	        // FIRE ALARM ACTIVE --> STOP SYSTEM
	        IF #ReadInputSignal.FireActive THEN
	            #StopSystemFireActive := TRUE;
	        END_IF;
	        // RESET --> FIRE ALARM
	        IF #SSR.ResetSystem AND NOT #ReadInputSignal.FireActive THEN
	            #StopSystemFireActive := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	END_REGION
	
	REGION Conveyor Status
	    
	    REGION Status ON
	        
	        // ----------------------------------------->>>>>>> STATUS ON <<<<<<<-----------------------------------------
	        // FB_ConveyorStatusON
	        // called as IDMB
	        
	        #TRANSFER_STATUS_ON(SystemStartActive := #SSR.StartSystem,
	                            StatusStop := #ReadTransferStatus.Stop,
	                            StatusEStop := #ReadTransferStatus.Estop,
	                            StatusManual := #ReadTransferStatus.Manual,
	                            StatusFault := #ReadTransferStatus.Fault,
	                            FireAlarmActive := #StopSystemFireActive,
	                            StatusON => #ReadTransferStatus.ON);
	        
	    END_REGION
	    
	    REGION Status Ready
	        
	        // ----------------------------------------->>>>>>> STATUS READY <<<<<<<-----------------------------------------
	        // FB_ConveyorStatusReady
	        // called as MIDB
	        
	        #TRANSFER_STATUS_READY(StatusON := #ReadTransferStatus.ON,
	                               StatusESTOP := #ReadTransferStatus.Estop,
	                               StatusFAULT := #ReadTransferStatus.Fault,
	                               StatusMAN := #ReadTransferStatus.Manual,
	                               SignalFIRE := #StopSystemFireActive,
	                               SystemAlertTime := #ReadTransferParameters.StartUpTime,
	                               StatusReady => #ReadTransferStatus.ReadytoRun);
	        
	    END_REGION
	    
	    REGION Status Occupied
	        
	        // ----------------------------------------->>>>>>> STATUS OCCUPIED <<<<<<<-----------------------------------------
	        
	        #ReadTransferStatus.Occupied :=
	        #FilteredSensor AND
	        NOT #FRONT_SENSOR_FILTER.FilteredSensor;
	        
	    END_REGION
	    
	    REGION Status Running
	        
	        // ----------------------------------------->>>>>>> STATUS RUNNING <<<<<<<-----------------------------------------
	        
	        #ReadTransferStatus.Running := #Transfer.Command_1 OR #Transfer.Command_2;
	        
	    END_REGION
	    
	    REGION Status Fault
	        
	        // Automatic reset trigger to the needed drive after wakeup from ESTOP power miss (only if needed)
	        // will trigger for every Reset active
	        
	        #AUTO_RESET(AckIN := #SSR.ResetSystem,
	                    Ack => #AutoReset);
	        
	        // ----------------------------------------->>>>>>> STATUS FAULT <<<<<<<-----------------------------------------
	        // FC_ConveyorStatusFault
	        #TRANSFER_STATUS_FAULT(FaultActive := #ReadInputSignal.Fault,
	                               ResetFault := #SSR.ResetSystem OR #AutoReset,
	                               StatusRunning := #ReadTransferStatus.Running,
	                               FaultStatus => #ReadTransferStatus.Fault);
	        
	        #ErrorReset := #SSR.ResetSystem;
	        
	    END_REGION
	    
	    REGION Status ESTOP
	        
	        // ----------------------------------------->>>>>>> STATUS ESTOP <<<<<<<-----------------------------------------
	        
	        // EMERGENCY STOP ACTIVE --> STOP SYSTEM
	        IF #ReadInputSignal.EmergencyStop THEN
	            #ReadTransferStatus.Estop := TRUE;
	        END_IF;
	        // RESET --> EMERGENCY STOP
	        IF #AutoReset AND NOT #ReadInputSignal.EmergencyStop THEN
	            #ReadTransferStatus.Estop := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Status Manual
	        
	        // ----------------------------------------->>>>>>> STATUS MANUAL <<<<<<<-----------------------------------------
	        
	        // Write to static from IO
	        // ACTIVATE --> MANUAL MODE
	        // Line A1
	        IF #Active.Line.A1 THEN
	            // SET --> MANUAL MODE
	            IF DB_Manual.Line.A1.Conveyor[#TransferID].ManualEnable THEN
	                #ReadTransferStatus.Manual := TRUE;
	            END_IF;
	            // RESET --> MANUAL MODE
	            IF NOT DB_Manual.Line.A1.Conveyor[#TransferID].ManualEnable AND #SSR.ResetSystem THEN
	                #ReadTransferStatus.Manual := FALSE;
	            END_IF;
	        END_IF;
	        // Line B1
	        IF #Active.Line.B1 THEN
	            // SET --> MANUAL MODE
	            IF DB_Manual.Line.B1.Conveyor[#TransferID].ManualEnable THEN
	                #ReadTransferStatus.Manual := TRUE;
	            END_IF;
	            // RESET --> MANUAL MODE
	            IF NOT DB_Manual.Line.B1.Conveyor[#TransferID].ManualEnable AND #SSR.ResetSystem THEN
	                #ReadTransferStatus.Manual := FALSE;
	            END_IF;
	        END_IF;
	        // Line C1
	        IF #Active.Line.C1 THEN
	            // SET --> MANUAL MODE
	            IF DB_Manual.Line.C1.Conveyor[#TransferID].ManualEnable THEN
	                #ReadTransferStatus.Manual := TRUE;
	            END_IF;
	            // RESET --> MANUAL MODE
	            IF NOT DB_Manual.Line.C1.Conveyor[#TransferID].ManualEnable AND #SSR.ResetSystem THEN
	                #ReadTransferStatus.Manual := FALSE;
	            END_IF;
	        END_IF;
	        // Line D1
	        IF #Active.Line.D1 THEN
	            // SET --> MANUAL MODE
	            IF DB_Manual.Line.D1.Conveyor[#TransferID].ManualEnable THEN
	                #ReadTransferStatus.Manual := TRUE;
	            END_IF;
	            // RESET --> MANUAL MODE
	            IF NOT DB_Manual.Line.D1.Conveyor[#TransferID].ManualEnable AND #SSR.ResetSystem THEN
	                #ReadTransferStatus.Manual := FALSE;
	            END_IF;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Status Stop
	        
	        // ----------------------------------------->>>>>>> STATUS STOP <<<<<<<-----------------------------------------
	        
	        IF #SSR.StopSystem THEN
	            #ReadTransferStatus.Stop := TRUE;
	        END_IF;
	        IF #SSR.StartSystem OR #SSR.ResetSystem THEN
	            #ReadTransferStatus.Stop := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Status Blockage
	        
	        // FB_SensorBlockage
	        // called as MIDB
	        #TRANSFER_SENSOR_BLOCKAGE(IN := #ReadTransferStatus.ReadytoRun AND #FilteredSensor AND NOT #Downstream,
	                                  PT := T#15s);
	        
	        #FRONT_SENSOR_BLOCKAGE(IN := #ReadTransferStatus.ReadytoRun AND #FrontSensor AND NOT #Downstream,
	                               PT := T#15s);
	        
	        #END_SENSOR_BLOCKAGE(IN := #ReadTransferStatus.ReadytoRun AND #EndSensor AND NOT #Downstream,
	                             PT := T#15s);
	        
	        #"LEFT/RIGHT_SENSOR_BLOCKAGE"(IN := #ReadTransferStatus.ReadytoRun AND #"Left/RightSensor" AND NOT #Downstream,
	                                      PT := T#5s);
	        
	        #ReadTransferStatus.Blockage :=
	        (#TRANSFER_SENSOR_BLOCKAGE.Q OR
	        #FRONT_SENSOR_BLOCKAGE.Q OR
	        #END_SENSOR_BLOCKAGE.Q OR
	        #"LEFT/RIGHT_SENSOR_BLOCKAGE".Q) AND
	        (#TransferDirection.Straight OR
	        #TransferDirection.toRight OR
	        #TransferDirection.toLeft);
	        
	        // ==========================
	        // Sensor Blockage: HMI Alarm
	        // ==========================
	        // Sensor blockage alarm message is written to the alarm word
	        // corresponding to the active line
	        
	        REGION alarm 
	            
	            // ==========================
	            // Sensor Blockage: HMI Alarm
	            // ==========================
	            // Sensor blockage alarm message is written to the alarm word
	            // corresponding to the active line
	            // Line A1
	            IF #Active.Line.A1 THEN
	                DB_Alarm."word".Line.A1.Conveyor[#TransferID].%X0 := #ReadTransferStatus.Blockage;
	            END_IF;
	            // Line B1
	            IF #Active.Line.B1 THEN
	                DB_Alarm."word".Line.B1.Conveyor[#TransferID].%X0 := #ReadTransferStatus.Blockage;
	            END_IF;
	            // Line C1
	            IF #Active.Line.C1 THEN
	                DB_Alarm."word".Line.C1.Conveyor[#TransferID].%X0 := #ReadTransferStatus.Blockage;
	            END_IF;
	            // Line D1
	            IF #Active.Line.D1 THEN
	                DB_Alarm."word".Line.D1.Conveyor[#TransferID].%X0 := #ReadTransferStatus.Blockage;
	            END_IF;
	            
	        END_REGION
	        
	    END_REGION
	    
	    REGION Visu Color
	        
	        // each and every status of the conveyor will be mentioned by different colors in HMI panel
	        
	        #VisuColor.%X8 := #ReadTransferStatus.Blockage;
	        #VisuColor.%X0 := #ReadTransferStatus.ON;
	        #VisuColor.%X1 := #ReadTransferStatus.ReadytoRun;
	        #VisuColor.%X2 := #ReadTransferStatus.Occupied;
	        #VisuColor.%X3 := #ReadTransferStatus.Running;
	        #VisuColor.%X4 := #ReadTransferStatus.Fault;
	        #VisuColor.%X5 := #ReadTransferStatus.Estop;
	        #VisuColor.%X6 := #ReadTransferStatus.Manual;
	        #VisuColor.%X7 := #ReadTransferStatus.Stop;
	        
	        // convert word to Interger value: 
	        // color will be depends on the status priority (integer value) 
	        // reference: Conveyor image in HMI screen -> property -> Apperance   
	        
	        // Line A1
	        IF #Active.Line.A1 THEN
	            DB_VisuColor.Line.A1.Conveyor[#TransferID] :=
	            WORD_TO_INT(#VisuColor);
	        END_IF;
	        // Line B1
	        IF #Active.Line.B1 THEN
	            DB_VisuColor.Line.B1.Conveyor[#TransferID] :=
	            WORD_TO_INT(#VisuColor);
	        END_IF;
	        // Line C1
	        IF #Active.Line.C1 THEN
	            DB_VisuColor.Line.C1.Conveyor[#TransferID] :=
	            WORD_TO_INT(#VisuColor);
	        END_IF;
	        // Line D1
	        IF #Active.Line.D1 THEN
	            DB_VisuColor.Line.D1.Conveyor[#TransferID] :=
	            WORD_TO_INT(#VisuColor);
	        END_IF;
	        
	    END_REGION
	    
	    REGION Global Status
	        
	        // write -> global status
	        // Line A1
	        IF #Active.Line.A1 THEN
	            DB_Global.Status.Line.A1.On[#TransferID] := #ReadTransferStatus.ON;
	            DB_Global.Status.Line.A1.ReadyToRun[#TransferID] := #ReadTransferStatus.ReadytoRun;
	            DB_Global.Status.Line.A1.Occupied[#TransferID] := #ReadTransferStatus.Occupied;
	            DB_Global.Status.Line.A1.Running[#TransferID] := #ReadTransferStatus.Running;
	            DB_Global.Status.Line.A1.Fault[#TransferID] := #ReadTransferStatus.Fault;
	            DB_Global.Status.Line.A1.Estop[#TransferID] := #ReadTransferStatus.Estop;
	            DB_Global.Status.Line.A1.Manual[#TransferID] := #ReadTransferStatus.Manual;
	            DB_Global.Status.Line.A1.Stop[#TransferID] := #ReadTransferStatus.Stop;
	            DB_Global.Status.Line.A1.Blockage[#TransferID] := #ReadTransferStatus.Blockage;
	        END_IF;
	        // Line B1
	        IF #Active.Line.B1 THEN
	            DB_Global.Status.Line.B1.On[#TransferID] := #ReadTransferStatus.ON;
	            DB_Global.Status.Line.B1.ReadyToRun[#TransferID] := #ReadTransferStatus.ReadytoRun;
	            DB_Global.Status.Line.B1.Occupied[#TransferID] := #ReadTransferStatus.Occupied;
	            DB_Global.Status.Line.B1.Running[#TransferID] := #ReadTransferStatus.Running;
	            DB_Global.Status.Line.B1.Fault[#TransferID] := #ReadTransferStatus.Fault;
	            DB_Global.Status.Line.B1.Estop[#TransferID] := #ReadTransferStatus.Estop;
	            DB_Global.Status.Line.B1.Manual[#TransferID] := #ReadTransferStatus.Manual;
	            DB_Global.Status.Line.B1.Stop[#TransferID] := #ReadTransferStatus.Stop;
	            DB_Global.Status.Line.B1.Blockage[#TransferID] := #ReadTransferStatus.Blockage;
	        END_IF;
	        // Line C1
	        IF #Active.Line.C1 THEN
	            DB_Global.Status.Line.C1.On[#TransferID] := #ReadTransferStatus.ON;
	            DB_Global.Status.Line.C1.ReadyToRun[#TransferID] := #ReadTransferStatus.ReadytoRun;
	            DB_Global.Status.Line.C1.Occupied[#TransferID] := #ReadTransferStatus.Occupied;
	            DB_Global.Status.Line.C1.Running[#TransferID] := #ReadTransferStatus.Running;
	            DB_Global.Status.Line.C1.Fault[#TransferID] := #ReadTransferStatus.Fault;
	            DB_Global.Status.Line.C1.Estop[#TransferID] := #ReadTransferStatus.Estop;
	            DB_Global.Status.Line.C1.Manual[#TransferID] := #ReadTransferStatus.Manual;
	            DB_Global.Status.Line.C1.Stop[#TransferID] := #ReadTransferStatus.Stop;
	            DB_Global.Status.Line.C1.Blockage[#TransferID] := #ReadTransferStatus.Blockage;
	        END_IF;
	        // Line D1
	        IF #Active.Line.D1 THEN
	            DB_Global.Status.Line.D1.On[#TransferID] := #ReadTransferStatus.ON;
	            DB_Global.Status.Line.D1.ReadyToRun[#TransferID] := #ReadTransferStatus.ReadytoRun;
	            DB_Global.Status.Line.D1.Occupied[#TransferID] := #ReadTransferStatus.Occupied;
	            DB_Global.Status.Line.D1.Running[#TransferID] := #ReadTransferStatus.Running;
	            DB_Global.Status.Line.D1.Fault[#TransferID] := #ReadTransferStatus.Fault;
	            DB_Global.Status.Line.D1.Estop[#TransferID] := #ReadTransferStatus.Estop;
	            DB_Global.Status.Line.D1.Manual[#TransferID] := #ReadTransferStatus.Manual;
	            DB_Global.Status.Line.D1.Stop[#TransferID] := #ReadTransferStatus.Stop;
	            DB_Global.Status.Line.D1.Blockage[#TransferID] := #ReadTransferStatus.Blockage;
	        END_IF;
	        
	    END_REGION
	    
	END_REGION
	
	REGION Transfer Logic
	    
	    REGION infeed receive condition
	        
	        // NegEdgeUpstream
	        #NegEdgeUpstream(CLK := #Upstream);
	        
	        // item will be send into the transfer section when there is no other item
	        
	        IF #ReadTransferStatus.ReadytoRun AND
	            #Upstream AND
	            NOT #ReadTransferStatus.Occupied THEN
	            #Transfer.RcvInfeed := TRUE;
	        END_IF;
	        
	        // receive infeed tag goes to FALSE when the item from the previous conveyor is completly out of the previous conveyor
	        IF NOT #Upstream(* #NegEdgeUpstream.Q *)THEN
	            #Transfer.RcvInfeed := FALSE;
	        END_IF;
	        
	        // Receive infeed
	        #Transfer.StartSignal := #Transfer.RcvInfeed;
	        
	    END_REGION
	    
	    REGION Transfer direction outputs
	        
	        IF NOT #EndSensor AND NOT #"Left/RightSensor" AND NOT #ReadTransferStatus.Running THEN
	            
	            #sTransferDirection := #TransferDirection;
	            
	        END_IF;
	        
	        // Transfer Straight
	        
	        IF #sTransferDirection.Straight THEN
	            
	            IF #ReadTransferStatus.ReadytoRun AND
	                NOT #Transfer.RcvInfeed AND
	                #ReadTransferStatus.Occupied AND
	                NOT #Downstream
	            THEN
	                #Transfer.Command_1 := TRUE;
	                #Transfer.Command_2 := TRUE;
	            END_IF;
	            
	        END_IF;
	        
	        // Transfer Right
	        
	        IF #sTransferDirection.toRight THEN
	            
	            IF #ReadTransferStatus.ReadytoRun AND
	                NOT #Transfer.RcvInfeed AND
	                #ReadTransferStatus.Occupied AND
	                NOT #Downstream
	            THEN
	                #Transfer.Command_1 := TRUE;
	                #Transfer.Command_2 := FALSE;
	            END_IF;
	            
	        END_IF;
	        
	        // Transfer Left
	        
	        IF #sTransferDirection.toLeft THEN
	            
	            IF #ReadTransferStatus.ReadytoRun AND
	                NOT #Transfer.RcvInfeed AND
	                #ReadTransferStatus.Occupied AND
	                NOT #Downstream
	            THEN
	                #Transfer.Command_1 := FALSE;
	                #Transfer.Command_2 := TRUE;
	            END_IF;
	            
	        END_IF;
	        
	    END_REGION
	    
	    REGION TRANSFER OFF
	        
	        // Transfer commands Off
	        
	        IF NOT #ReadTransferStatus.ReadytoRun OR
	            #Transfer.RcvInfeed OR
	            (#ReadTransferStatus.Occupied AND
	            #Downstream) OR
	            NOT #ReadTransferStatus.Occupied
	        THEN
	            #Transfer.Command_1 := FALSE;
	            #Transfer.Command_2 := FALSE;
	        END_IF;
	        
	    END_REGION
	    
	    REGION Write output
	        
	        // Static to output
	        
	        #StartSignal := #Transfer.StartSignal;
	        #Cmd1 := #Transfer.Command_1;
	        #Cmd2 := #Transfer.Command_2;
	        
	    END_REGION
	    
	END_REGION
	
	
	
	
	
END_FUNCTION_BLOCK
END_NAMESPACE

